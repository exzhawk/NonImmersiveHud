buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"

    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'


version = '1.0'
group = 'me.exz.nonimmersivehud' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'nonimmersivehud'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

repositories {
    jcenter()
    maven {
        url "http://maven.shadowfacts.net/"
    }
    maven {url "http://maven.tehnut.info/"}
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replaceIn "me/exz/nonimmersivehud/Reference.kt"
    replace "@VERSION@", project.version
}
configurations {
    compile.extendsFrom shadow
}
dependencies {
    deobfCompile("mcp.mobius.waila:Hwyla:${hwyla_version}")
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.8.4"
    shadow "org.jetbrains.kotlin:kotlin-stdlib"
    shadow "org.jetbrains.kotlin:kotlin-reflect"
    shadow "org.eclipse.jetty:jetty-server:${jetty_version}"
    shadow "org.eclipse.jetty:jetty-servlet:${jetty_version}"
    shadow "org.eclipse.jetty.websocket:websocket-server:${jetty_version}"
}
shadowJar {
    configurations = [project.configurations.shadow]

    relocate "org.eclipse.jetty", "me.exz.nonimmersivehud.repack.org.eclipse.jetty"
    relocate "javax.servlet", "me.exz.nonimmersivehud.repack.javax.servlet"
    relocate "kotlin", "me.exz.nonimmersivehud.repack.kotlin"

    classifier = null
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}
tasks.reobfShadowJar.mustRunAfter shadowJar

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
